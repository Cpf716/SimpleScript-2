//  Author: Corey Ferguson
//  Date:   August 3, 2023
//  File:   dateString.txt
//

include ((dir = "@/datetime/data/") + "months")
include (dir + "weekdays")
include "@/datetime/date/weekday"
include "@/string/padStart"

func main
    if nrows(argv) !== 2
        expectExcept("1 argument(s), got {}" format (nrows(argv) - 1))
    end if

    timestamp = (argv row 1) slice 1

    if subtypeOf timestamp !== "int"
        typeExcept(subtypeOf timestamp, "int")
    end if

    if timestamp < 0
        rangeExcept(string(timestamp))
    end if
        
    `weekday` = (weekdays().(weekday(timestamp)) slice 0, 3)

    seconds = 0, secondsInYear = 0, year = 1970
    while timestamp >= seconds + (secondsInYear = (365 + isLeapYear(year)) * 86400)
        seconds += secondsInYear
        year += 1
    end while

    timestamp -= seconds

    seconds = 0, secondsInMonth = 0, month = 0
    while timestamp >= seconds + (secondsInMonth = (months().(month * 2 + 1) + (month === 1 && isLeapYear(year))) * 86400)
        seconds += secondsInMonth
        month += 1
    end while

    timestamp -= seconds

    day = 0
    while timestamp >= (day + 1) * 86400
        day += 1
    end while

    timestamp -= 86400 * day

    day += 1

    hours = 0
    while timestamp >= (hours + 1) * 3600
        hours += 1
    end while

    timestamp -= hours * 3600

    minutes = 0
    while timestamp >= (minutes + 1) * 60
        minutes += 1
    end while

    timestamp -= minutes * 60

    seconds = timestamp
    
    time = (array(hours, minutes, seconds) map val, padStart(string(val), 2, "0")) join ":"

    array dateStr = `weekday`
    
    dateStr += (((keys months()).month) slice 0, 3)
    dateStr += day
    dateStr += time
    dateStr += year

    return dateStr join " "
end func

return main()

