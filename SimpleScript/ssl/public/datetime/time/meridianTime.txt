//  Author: Corey Ferguson
//  Date:   August 31, 2023
//  File:   meridianTime.txt
//

include ((dir = "@/datetime/") + "data/months")
include (dir + "date/isLeapYear")
include "@/string/padStart"

func main
    if nrows(argv) !== 2
        expectError("Expected 1 argument(s), got {}" format (nrows(argv) - 1))
    end if

    timestamp = (argv row 1) slice 1

    if subtypeOf timestamp !== "int"
        typeError(subtypeOf timestamp, "int")
    end if

    if timestamp < 0
        rangeError(string(timestamp))
    end if

    seconds = 0, year = 1970
    while timestamp >= seconds + (365 + isLeapYear(year)) * 86400
        seconds += (365 + isLeapYear(year)) * 86400
        year += 1
    end while

    timestamp -= seconds

    seconds = 0, month = 0
    while timestamp >= seconds + (months().(month * 2 + 1) + (month === 1 && isLeapYear(year))) * 86400
        seconds += (months().(month * 2 + 1) + (month === 1 && isLeapYear(year))) * 86400
        month += 1
    end while

    timestamp -= seconds

    day = 0
    while timestamp >= (day + 1) * 86400
        day += 1
    end while

    timestamp -= 86400 * day

    hours = 0
    while timestamp >= (hours + 1) * 3600
        hours += 1
    end while

    timestamp -= hours * 3600

    minutes = 0
    while timestamp >= (minutes + 1) * 60
        minutes += 1
    end while

    timestamp -= minutes * 60

    seconds = timestamp
    
    isAfternoon = false
    
    if hours >= 12
        isAfternoon = true
    
        if hours > 12
            hours -= 12
        end if
    end if
    
    time = (array(hours, minutes, seconds) map val, padStart(string(val), 2, "0")) join ":"
    
    return array(time, (isAfternoon ? "PM", "AM")) join " "
end func

return main()



