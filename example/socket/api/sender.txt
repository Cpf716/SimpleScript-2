//  Author: Corey Ferguson
//  Date:   September 24, 2023
//  File:   apiServer.txt
//

include("@/datetime/parseTime")

func mysql : sql
    con = -1

    try
        con = connect("tcp://localhost:3306", "root", "root")
        
        setSchema(con, "music_library")
    catch err
        echo "SQL: {}\n" format err

        return
    end try
    
    echo "\n{}\n\n" format sql
    
    array res = null

    try
        res = query(con, sql)
    catch err
        echo "SQL: {}\n" format err
    end try
    
    try
        close(con)
    catch err
        echo "SQL: {}\n" format err
    end try
    
    res = inverse res

    res insert 1, (array(first res) fill (count res - 1) / first res)
    
    num = parseTime(gmt())
    
    path = "/tmp/{}.csv" format num

    write(path, inverse res, "csv")
    
    res = read(path)
    
    remove(path)
    
    return res
end func

func main
    sfd = server(8080, 1024)

    echo "Server listening on port 8080...\n"

    while 1
        array conv = null
        while !(conv = accept(sfd))
            continue
        end while
        
        echo "Connected...\n"
        
        cfd = first conv
        
        req = recv(cfd)
        
        if req === "/api/artists"
            send(cfd, mysql("select * from artists"))
            
        else if req === "/api/albums"
            send(cfd, mysql("select * from albums"))
            
        else if req === "/api/songs"
            send(cfd, mysql("select * from songs"))
            
        else if req === "/api/join"
            send(cfd, mysql("select artist_name, album_year, album_name, song_title from songs join albums on songs.album_id = albums.album_id join artists on albums.artist_id = artists.artist_id"))
            
        else if count req
            send(cfd, "-404")
        end if
        
        close(cfd)
        
        echo "Disconnected.\n"
    end while
end func

main()
